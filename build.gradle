buildscript {

  ext.versions = ['minSdk'        : 15,
                  'compileSdk'    : 27,
                  'androidPlugin' : '3.1.2',
                  'supportLibrary': '27.0.2',
                  'dagger'        : '2.11',
                  'retrofit'      : '2.3.0',
                  'stetho'        : '1.5.0',
                  'okhttp'        : '3.8.1',
                  'rxAndroid'     : '2.0.1',
                  'glide'         : '4.6.1',
                  'gson'          : '2.8.1',
                  'arch'          : '1.1.0',
                  'lonelysword'   : '0.2.3']

  ext.deps = [android      : ['runtime'     : 'com.google.android:android:4.1.1.4',
                              'gradlePlugin': "com.android.tools.build:gradle:${versions.androidPlugin}",],
              'support'    : ['compat'      : "com.android.support:support-compat:${versions.supportLibrary}",
                              'appcompat'   : "com.android.support:appcompat-v7:${versions.supportLibrary}",
                              'recyclerview': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                              'design'      : "com.android.support:design:${versions.supportLibrary}",
                              'cardview'    : "com.android.support:cardview-v7:${versions.supportLibrary}",
                              'supportV4'   : "om.android.support:support-v4:${versions.supportLibrary}",
                              'annotations' : "com.android.support:support-annotations:${versions.supportLibrary}",
                              'test'        : ['runner': 'com.android.support.test:runner:1.0.1',],],

              'dagger'     : [compiler     : "com.google.dagger:dagger-compiler:${versions.dagger}",
                              android      : "com.google.dagger:dagger-android:${versions.dagger}",
                              processor    : "com.google.dagger:dagger-android-processor:${versions.dagger}",
                              daggerSupport: "com.google.dagger:dagger-android-support:${versions.dagger}"],

              'retrofit'   : [retrofit : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                              converter: "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
                              adapter  : "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}",
                              logger   : "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}",
                              stetho   : "com.facebook.stetho:stetho-okhttp3:${versions.stetho}",],

              'rx'         : [rxAndroid: "io.reactivex.rxjava2:rxandroid:${versions.rxAndroid}"],

              'gson'       : [gson: "com.google.code.gson:gson:${versions.gson}"],

              'glide'      : [glide   : "com.github.bumptech.glide:glide:${versions.glide}",
                              compiler: "com.github.bumptech.glide:compiler:${versions.glide}"],

              'arch'       : [extensions: "android.arch.lifecycle:extensions:${versions.arch}",
                              runtime   : "android.arch.lifecycle:runtime:${versions.arch}",
                              compiler  : "android.arch.lifecycle:compiler:${versions.arch}"],

              'lonelysword': [lonelysword: "com.timper:lonelysword:${versions.lonelysword}",
                              usecase    : "com.timper:lonelysword-usecase:${versions.lonelysword}",
                              dagger     : "com.timper:lonelysword-dagger:${versions.lonelysword}",
                              compiler   : "com.timper:lonelysword-compiler:${versions.lonelysword}"]

  ]

  repositories {
    google()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven {
      url 'https://maven.google.com'
    }
  }

  dependencies {
    classpath deps.android.gradlePlugin
    classpath 'com.jakewharton:butterknife-gradle-plugin:9.0.0-SNAPSHOT'
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven {
      url 'https://maven.google.com'
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
subprojects {
  project.configurations.all {
    resolutionStrategy.eachDependency { details ->
      if (details.requested.group == 'com.android.support' && !details.requested.name.contains('multidex')) {
        details.useVersion "27.0.2"
      }
    }
  }
}
